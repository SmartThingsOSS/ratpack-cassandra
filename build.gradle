buildscript {
	repositories {
		jcenter()
		gradlePluginPortal()
	}

	dependencies {
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
	}
}


def jvmEncoding = java.nio.charset.Charset.defaultCharset().name()
if (jvmEncoding != "UTF-8") {
	throw new IllegalStateException("Build environment must be UTF-8 (it is: $jvmEncoding) - add '-Dfile.encoding=UTF-8' to the GRADLE_OPTS environment variable ")
}

if (!JavaVersion.current().java8Compatible) {
	throw new IllegalStateException("Must be built with Java 8 or higher")
}

subprojects {


	group = "smartthings"
	version = rootProject.file('version.txt').text.trim()

	apply plugin: "base"
	apply plugin: "java"
	apply plugin: "groovy"
	apply plugin: "maven-publish"
	apply plugin: "jacoco"
	apply plugin: 'idea'
	apply plugin: 'com.github.ben-manes.versions'
	apply from: rootProject.file('gradle/publishing.gradle')


	sourceCompatibility = "1.8"
	targetCompatibility = "1.8"

	repositories {
		mavenLocal()
		jcenter()
	}

	dependencies {
		testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
		testRuntimeOnly('cglib:cglib-nodep:3.3.0')
		testRuntimeOnly('org.objenesis:objenesis:3.2')
	}
	test {
		useJUnitPlatform()
		testLogging {
			events "passed", "skipped", "failed"
		}
	}

	jacocoTestReport {
		reports {
			xml.enabled true
		}
	}
}
